buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.10.1"
    }
}

apply plugin: "maven"
apply plugin: "java-gradle-plugin"
apply plugin: "com.gradle.plugin-publish"

group = "org.embulk"
archivesBaseName = "${project.name}"
version = "0.2.0-SNAPSHOT"
description = "A Gradle plugin to build and publish Embulk plugins"

repositories {
    mavenCentral()
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

dependencies {
    implementation gradleApi()

    testImplementation gradleTestKit()
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.5.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.5.0"
}

test {
    useJUnitPlatform()
    testLogging {
        outputs.upToDateWhen { false }
        showStandardStreams = true
    }
}

jar {
    from rootProject.file("LICENSE")
}

gradlePlugin {
    plugins {
        embulkPluginsPlugin {
            id = "org.embulk.embulk-plugins"
            displayName = "A Gradle plugin for Embulk plugins"
            description = "${project.description}"
            implementationClass = "org.embulk.gradle.embulk_plugins.EmbulkPluginsPlugin"
        }
    }
}

pluginBundle {
    website = "http://www.embulk.org/"
    vcsUrl = "https://github.com/embulk/gradle-embulk-plugins"
    tags = ["embulk"]
}
